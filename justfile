# Cross-platform shell configuration
# Use PowerShell on Windows (higher precedence than shell setting)
set windows-shell := ["powershell.exe", "-NoLogo", "-Command"]
# Use sh on Unix-like systems
set shell := ["sh", "-c"]

[doc("All command information")]
default:
  @just --list --unsorted --list-heading $'Telegramus  commands…\n'

# Linter
[doc("Ruff format")]
[group("linter")]
ruff-format *params:
  uv run --active --frozen ruff format {{params}}

[doc("Ruff check")]
[group("linter")]
ruff-check *params:
  uv run --active --frozen ruff check --exit-non-zero-on-fix {{params}}

_codespell:
  uv run --active --frozen codespell -L Dependant,dependant,selectin,aadd

[doc("Check typos")]
[group("linter")]
typos: _codespell
  uv run --active --frozen prek run --all-files typos

[doc("Linter run")]
[group("linter")]
linter: ruff-format ruff-check _codespell

# Static analysis
[doc("Mypy check")]
[group("static analysis")]
mypy *params:
  uv run --active --frozen mypy {{params}}

[doc("Bandit check")]
[group("static analysis")]
bandit:
  uv run --active --frozen bandit -c pyproject.toml -r src

[doc("Semgrep check")]
[group("static analysis")]
semgrep:
  uv run --active --frozen semgrep scan --config auto --error --skip-unknown-extensions src

[doc("Zizmor check")]
[group("static analysis")]
zizmor:
  uv run --active --frozen zizmor .

[doc("Architecture checks with import-linter")]
[group("static analysis")]
import-linter *params:
  uv run --active --frozen lint-imports {{params}}

[doc("Static analysis check")]
[group("static analysis")]
static-analysis: mypy bandit semgrep import-linter

[doc("Run pytest with coverage")]
[group("tests")]
test:
  uv run --active --frozen pytest --cov=src/goldy --cov-report=term-missing

[doc("Unit tests only (no Docker required)")]
[group("tests")]
test-unit *params:
  uv run --active --frozen pytest tests/unit {{params}}

[doc("Integration tests only (needs Docker)")]
[group("tests")]
test-integration *params:
  uv run --active --frozen pytest tests/integration {{params}}

# Emits coverage.xml and junit.xml, which sonar-project.properties points at.
[doc("Run the full suite the way CI does")]
[group("tests")]
test-ci:
  uv run --active --frozen pytest --cov=src/goldy --cov-report=xml --cov-report=term-missing --junitxml=junit.xml

[doc("Everything CI runs, in CI's order")]
[group("tests")]
ci: linter static-analysis test-ci

# Docker
[doc("Build the production image")]
[group("docker")]
docker-build:
  docker build -f deploy/prod/answer_service/Dockerfile -t answer-service:local .

# Must be `.env` at the root: `env_file:` only reaches the containers, while
# ${VAR:?} interpolation reads the project env file compose finds by that name.
[doc("Start the local environment (postgres, nats, redis, qdrant, app)")]
[group("docker")]
up *params:
  docker compose up -d {{params}}

[doc("Start only the backing services, for running the app on the host")]
[group("docker")]
up-deps:
  docker compose up -d postgres nats redis qdrant

[doc("Start the dev backing services (throwaway defaults, no .env needed)")]
[group("docker")]
up-dev:
  docker compose -f docker-compose.dev.yaml up -d

[doc("Stop and remove the dev backing services and their volumes")]
[group("docker")]
down-dev:
  docker compose -f docker-compose.dev.yaml down -v

[doc("Stop the local environment")]
[group("docker")]
down *params:
  docker compose down {{params}}

[doc("Tail the logs of the application services")]
[group("docker")]
logs *params:
  docker compose logs -f {{params}}

[doc("Validate the compose file against .env")]
[group("docker")]
compose-config:
  docker compose config --quiet

[doc("Pre-commit modified files")]
[group("pre-commit")]
pre-commit:
  uv run --active --frozen prek run

[doc("Pre-commit all files")]
[group("pre-commit")]
pre-commit-all:
  uv run --active --frozen prek run --all-files

# Migrations
[doc("Generate a new Alembic migration (usage: just migration 'add users table')")]
[group("migrations")]
migration msg:
  uv run --active dotenv -f .env run -- alembic revision --autogenerate -m "{{msg}}"

[doc("Apply all pending Alembic migrations")]
[group("migrations")]
migrate:
  uv run --active dotenv -f .env run -- alembic upgrade head

[doc("Roll back the last Alembic migration")]
[group("migrations")]
migrate-down:
  uv run --active dotenv -f .env run -- alembic downgrade -1

[doc("Show current migration revision")]
[group("migrations")]
migrate-current:
  uv run --active dotenv -f .env run -- alembic current
